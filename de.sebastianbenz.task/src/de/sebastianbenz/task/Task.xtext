grammar de.sebastianbenz.task.Task

import "platform:/resource/de.sebastianbenz.task/model/Task.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TaskModel:
	{TaskModel}
	(contents += Content)*
	WS*;
/*
Project
WS TASK
WS PROJECT
WS WS TASK
WS WS PROJECT 
 */

Content:
	Project |
	Task |  
	Note |
	EmptyLine | 
	Code
	;  

Task:
	(intend+=WS)* text=TASK_TEXT;

Note:
	(intend+=WS)* text=TEXT;

Project:
	(intend+=WS)* text=PROJECT_
;

Code:
	(intend+=WS)* text=CODE_
;


EmptyLine:
	text = Spaces
;

Spaces returns ecore::EString :
	(WS)* NL
;
	
terminal WS				:	' ' | '\t';
terminal NL				:	'\r'? '\n';
terminal CODE_			: "'''" -> "'''" NL?;	
	
terminal TASK_TEXT		: HYPHEN !('\n'|'\r')* NL?;
terminal TEXT			:  (!(HYPHEN | COLON | '\n'|'\r' | WS) | !(HYPHEN|'\n'|'\r' | WS) !('\n'|'\r')* !(COLON | '\n'|'\r')) NL?;
terminal PROJECT_		: !('\n'|'\r'|HYPHEN|WS) !('\n'|'\r')* COLON NL?;		
terminal fragment COLON : ':';

terminal fragment HYPHEN: '-';
	


