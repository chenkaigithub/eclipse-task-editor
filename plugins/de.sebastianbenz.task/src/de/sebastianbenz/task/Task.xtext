grammar de.sebastianbenz.task.Task

import "platform:/resource/de.sebastianbenz.task/model/Task.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TaskModel:
	{TaskModel}
	(contents += Content)*
	Ws*;
/*
Project
Ws TASK
Ws PROJECT
Ws Ws TASK
Ws Ws PROJECT 
 */

Content:
	Project |
	Task |  
	Note |
	EmptyLine | 
	Code
	;  

Task:
	intend=Intendation text=TASK_TEXT;

Note:
	intend=Intendation text=TEXT;

Project:
	intend=Intendation text=PROJECT_
;

Code:
	intend=Intendation text=CODE_
;

Intendation:
	(((SPACE SPACE) | TAB)* SPACE?)
;

EmptyLine:
	text = Spaces
;

Spaces:
	(Ws)* NL
;
	
Ws				:	SPACE | TAB;

terminal SPACE			:	' ';
terminal TAB			:	'\t';
terminal NL				:	'\r'? '\n';
terminal CODE_			: "'''" -> "'''" NL?;	
	
terminal TASK_TEXT		: HYPHEN !('\n'|'\r')* NL?;
terminal TEXT			: (!(HYPHEN | COLON | '\n'|'\r'|SPACE|TAB) | !(HYPHEN|'\n'|'\r'|SPACE|TAB) !('\n'|'\r')* !(COLON | '\n'|'\r')) NL?;
terminal PROJECT_		: !('\n'|'\r'|HYPHEN|SPACE|TAB) !('\n'|'\r')* COLON NL?;		
terminal fragment COLON : ':';

terminal fragment HYPHEN: '-';
	


